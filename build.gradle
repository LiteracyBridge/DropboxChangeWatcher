apply plugin: 'java'
apply plugin: 'application'

mainClassName = "org.literacybridge.dcp.DropboxChangeProcessor"

sourceCompatibility = 1.7
version = '1.0'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile group: 'com.dropbox.core', name: 'dropbox-core-sdk', version: '[1.7, 1.8)'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    systemProperties = [ testKeyFile: System.getProperty("testKeyFile") ]
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s')
    }
}
